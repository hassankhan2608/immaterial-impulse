import qs.services
import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Qt.labs.synchronizer
import Qt5Compat.GraphicalEffects
import Quickshell.Io
import Quickshell
import Quickshell.Wayland
import Quickshell.Hyprland

Scope { // Scope
    id: root
    property var tabButtonList: [
        {
            "icon": "keyboard",
            "name": Translation.tr("Keybinds")
        },
        {
            "icon": "experiment",
            "name": Translation.tr("Elements")
        },
    ]

    Loader {
        id: cheatsheetLoader
        active: false

        sourceComponent: PanelWindow { // Window
            id: cheatsheetRoot
            visible: cheatsheetLoader.active

            anchors {
                top: true
                bottom: true
                left: true
                right: true
            }

            // The binding currently open in the keybind editor overlay, null
            // when the overlay is closed.
            property var editingBinding: null

            function hide() {
                cheatsheetLoader.active = false;
            }
            exclusiveZone: 0
            implicitWidth: cheatsheetBackground.width + Appearance.sizes.elevationMargin * 2
            implicitHeight: cheatsheetBackground.height + Appearance.sizes.elevationMargin * 2
            WlrLayershell.namespace: "quickshell:cheatsheet"
            // Hyprland 0.49: Focus is always exclusive and setting this breaks mouse focus grab
            // WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
            color: "transparent"

            mask: Region {
                item: cheatsheetBackground
            }

            Component.onCompleted: {
                GlobalFocusGrab.addDismissable(cheatsheetRoot);
            }
            Component.onDestruction: {
                GlobalFocusGrab.removeDismissable(cheatsheetRoot);
            }
            Connections {
                target: GlobalFocusGrab
                function onDismissed() {
                    cheatsheetRoot.hide();
                }
            }

            // Scope the compositor's blur to the painted card so the drop
            // shadow below stays crisp instead of being frosted along with it
            // (#82, #89); pairs with rules.lua turning the layerrule blur off
            // for this namespace.
            WindowBlurRegion {
                targetWindow: cheatsheetRoot
                regionItem: cheatsheetBackground
                regionRadius: cheatsheetBackground.radius
            }

            // Background
            StyledRectangularShadow {
                target: cheatsheetBackground
            }
            Rectangle {
                id: cheatsheetBackground
                anchors.centerIn: parent
                color: Appearance.colors.colLayer0
                border.width: 1
                border.color: Appearance.colors.colLayer0Border
                radius: Appearance.rounding.windowRounding
                property real padding: Appearance.spacing.space250
                implicitWidth: cheatsheetColumnLayout.implicitWidth + padding * 2
                implicitHeight: cheatsheetColumnLayout.implicitHeight + padding * 2

                Keys.onPressed: event => { // Esc to close
                    if (event.key === Qt.Key_Escape) {
                        // Peel the editor overlay first; only a second Esc
                        // closes the cheatsheet itself.
                        if (cheatsheetRoot.editingBinding !== null) {
                            cheatsheetRoot.editingBinding = null;
                            event.accepted = true;
                            return;
                        }
                        cheatsheetRoot.hide();
                    }
                    if (event.modifiers === Qt.ControlModifier) {
                        if (event.key === Qt.Key_PageDown) {
                            tabBar.incrementCurrentIndex();
                            event.accepted = true;
                        } else if (event.key === Qt.Key_PageUp) {
                            tabBar.decrementCurrentIndex();
                            event.accepted = true;
                        } else if (event.key === Qt.Key_Tab) {
                            tabBar.setCurrentIndex((tabBar.currentIndex + 1) % root.tabButtonList.length);
                            event.accepted = true;
                        } else if (event.key === Qt.Key_Backtab) {
                            tabBar.setCurrentIndex((tabBar.currentIndex - 1 + root.tabButtonList.length) % root.tabButtonList.length);
                            event.accepted = true;
                        }
                    }
                }

                RippleButton { // Close button
                    id: closeButton
                    focus: cheatsheetRoot.visible
                    implicitWidth: 40
                    implicitHeight: 40
                    buttonRadius: Appearance.rounding.full
                    anchors {
                        top: parent.top
                        right: parent.right
                        topMargin: 20
                        rightMargin: 20
                    }

                    onClicked: {
                        cheatsheetRoot.hide();
                    }

                    contentItem: MaterialSymbol {
                        verticalAlignment: Text.AlignVCenter
                        anchors.centerIn: parent
                        horizontalAlignment: Text.AlignHCenter
                        font.pixelSize: Appearance.font.pixelSize.title
                        text: "close"
                    }
                }

                ColumnLayout { // Real content
                    id: cheatsheetColumnLayout
                    anchors.centerIn: parent
                    spacing: Appearance.spacing.space125

                    Toolbar {
                        Layout.alignment: Qt.AlignHCenter
                        enableShadow: false
                        ToolbarTabBar {
                            id: tabBar
                            tabButtonList: root.tabButtonList

                            Synchronizer on currentIndex {
                                property alias source: swipeView.currentIndex
                            }
                        }
                    }

                    SwipeView { // Content pages
                        id: swipeView
                        Layout.topMargin: Appearance.spacing.space50
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        spacing: Appearance.spacing.space125
                        currentIndex: Persistent.states.cheatsheet.tabIndex
                        onCurrentIndexChanged: {
                            Persistent.states.cheatsheet.tabIndex = currentIndex;
                        }

                        implicitWidth: Math.max.apply(null, contentChildren.map(child => child.implicitWidth || 0))
                        implicitHeight: Math.max.apply(null, contentChildren.map(child => child.implicitHeight || 0))

                        clip: true
                        layer.enabled: true
                        layer.effect: OpacityMask {
                            maskSource: Rectangle {
                                width: swipeView.width
                                height: swipeView.height
                                radius: Appearance.rounding.small
                            }
                        }

                        CheatsheetKeybinds {
                            // The room the card may use before it starts
                            // growing past the screen, minus the toolbar and
                            // padding - what decides the column count.
                            maxContentHeight: (cheatsheetRoot.screen?.height ?? 1080) - 220
                            // And the room across. Columns trade height for
                            // width, so a screen with height to spare but not
                            // width was asked for more columns than fit and the
                            // outer ones ran off both edges.
                            maxContentWidth: (cheatsheetRoot.screen?.width ?? 1920)
                                - Appearance.sizes.elevationMargin * 2
                                - cheatsheetBackground.padding * 2
                                - Appearance.spacing.space250 * 2
                            onEditRequested: bindingData => {
                                cheatsheetRoot.editingBinding = bindingData;
                            }
                        }
                        CheatsheetPeriodicTable {}
                    }
                }

                Rectangle { // Keybind editor overlay
                    id: keybindEditorScrim
                    anchors.fill: parent
                    radius: cheatsheetBackground.radius
                    color: Appearance.colors.colScrim
                    visible: cheatsheetRoot.editingBinding !== null
                    z: 10

                    MouseArea { // Click outside the card to dismiss
                        anchors.fill: parent
                        onClicked: cheatsheetRoot.editingBinding = null
                    }

                    Rectangle {
                        id: keybindEditorCard
                        anchors.centerIn: parent
                        property real padding: Appearance.spacing.space300
                        width: Math.min(560, keybindEditorScrim.width - Appearance.spacing.space800)
                        height: keybindEditorContent.implicitHeight + padding * 2
                        // colLayer1 carries the user's global transparency, which
                        // is right for a panel resting on the shell background and
                        // wrong for a modal resting on a dense keybind table: the
                        // rows underneath read straight through the dialog. The
                        // *Base* colour is the same surface, opaque.
                        color: Appearance.colors.colLayer1Base
                        border.width: 1
                        border.color: Appearance.colors.colLayer0Border
                        radius: Appearance.rounding.normal

                        MouseArea { // Swallow clicks inside the card
                            anchors.fill: parent
                        }

                        KeybindEditor {
                            id: keybindEditorContent
                            anchors {
                                top: parent.top
                                left: parent.left
                                right: parent.right
                                margins: keybindEditorCard.padding
                            }
                            bindingData: cheatsheetRoot.editingBinding
                            onDone: cheatsheetRoot.editingBinding = null
                        }
                    }

                    onVisibleChanged: {
                        if (visible)
                            keybindEditorContent.focusCapture();
                    }
                }
            }
        }
    }

    IpcHandler {
        target: "cheatsheet"

        function toggle(): void {
            cheatsheetLoader.active = !cheatsheetLoader.active;
        }

        function close(): void {
            cheatsheetLoader.active = false;
        }

        function open(): void {
            cheatsheetLoader.active = true;
        }
    }

    GlobalShortcut {
        name: "cheatsheetToggle"
        description: "Toggles cheatsheet on press"

        onPressed: {
            cheatsheetLoader.active = !cheatsheetLoader.active;
        }
    }

    GlobalShortcut {
        name: "cheatsheetOpen"
        description: "Opens cheatsheet on press"

        onPressed: {
            cheatsheetLoader.active = true;
        }
    }

    GlobalShortcut {
        name: "cheatsheetClose"
        description: "Closes cheatsheet on press"

        onPressed: {
            cheatsheetLoader.active = false;
        }
    }
}
